apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'groovyx.android'

def gitCommitHash = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId 'com.ingloriousmind.android.imtimetracking'
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 6
        versionName '1.2.0'

        buildConfigField "String", "GIT_COMMIT_HASH", "\"${gitCommitHash}\""
    }
    signingConfigs {
        release {
        }
    }
    if (project.hasProperty('storeFile') &&
            project.hasProperty('storePass') &&
            project.hasProperty('keyAlias') &&
            project.hasProperty('keyPass')) {
        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePass
        android.signingConfigs.release.keyAlias = keyAlias
        android.signingConfigs.release.keyPassword = keyPass
    } else {
        buildTypes.release.signingConfig = null
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        abortOnError false
    }
}

ext {
    supportLibVersion = '27.1.0'
    daggerVersion = '2.22.1'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'

    implementation "com.google.dagger:dagger-android:${daggerVersion}"
    implementation "com.google.dagger:dagger-android-support:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-android-processor:${daggerVersion}"

    implementation 'io.reactivex:rxandroid:1.2.0'
    implementation 'io.reactivex:rxjava:1.1.5'
    implementation 'com.esotericsoftware:kryo:4.0.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.j256.ormlite:ormlite-android:4.48'

    testImplementation 'org.codehaus.groovy:groovy-all:2.4.15'
    testImplementation 'org.codehaus.groovy:groovy:2.4.6:grooid'
    testImplementation 'org.spockframework:spock-core:1.0-groovy-2.4'
    testImplementation 'cglib:cglib-nodep:3.2.2'
    testImplementation 'org.objenesis:objenesis:2.6'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

apply from: 'quality.gradle'